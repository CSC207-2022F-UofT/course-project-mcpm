import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
    id 'checkstyle'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group 'org.hydev.csc207'
version '0.1.0.51'

repositories {
    mavenCentral()

    // Spigot MC
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(19))
    }
}

checkstyle {
    // assign the latest checkstyle version explicitly, since the default version is very old
    toolVersion = '10.3.4'
    configFile = rootProject.file('.github/checkstyle.xml')

    ignoreFailures = false
    maxWarnings = 5
}


dependencies {
    // The Spigot API with no shadowing. Requires the OSS repo.
    compileOnly 'org.spigotmc:spigot-api:1.19-R0.1-SNAPSHOT'

    // https://mvnrepository.com/artifact/net.sourceforge.argparse4j/argparse4j
    implementation 'net.sourceforge.argparse4j:argparse4j:0.9.0'

    // Apache HTTP Client
    // https://mvnrepository.com/artifact/org.apache.httpcomponents.client5/httpclient5-fluent
    implementation 'org.apache.httpcomponents.client5:httpclient5-fluent:5.2-beta1'

    // Json parser: Jackson
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.0-rc3'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-annotations
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.14.0-rc3'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0-rc3'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-yaml
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.0-rc3'

    // https://mvnrepository.com/artifact/com.google.guava/guava
    implementation 'com.google.guava:guava:31.1-jre'

    // YAML parser
    // https://mvnrepository.com/artifact/org.yaml/snakeyaml
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.33'

    // IDE annotations
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    implementation 'org.jetbrains:annotations:16.0.1'

    // https://mvnrepository.com/artifact/org.fusesource.jansi/jansi
    implementation 'org.fusesource.jansi:jansi:2.4.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
    duplicatesStrategy = 'include'
}

publishing {
    repositories {
        maven {
            name = "github"
            url = "https://maven.pkg.github.com/CSC207-2022F-UofT/mcpm"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        register("jar", MavenPublication) {
            from(components["java"])
            pom {
                url.set("https://github.com/CSC207-2022F-UofT/mcpm.git")
            }
        }
    }
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

sourceSets {
    test {
        resources {
            srcDir "test"
        }
    }
}

task printCp {
    doLast {
        System.err.println sourceSets.test.runtimeClasspath.asPath
    }
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}
